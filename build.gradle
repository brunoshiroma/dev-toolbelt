plugins {
	id 'org.springframework.boot' version '3.5.6'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'java'
	id 'org.graalvm.buildtools.native' version '0.11.0'
}

group = 'com.brunoshiroma'
version = '0.2.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

repositories {
	mavenCentral()
}

dependencies {
	implementation('org.springframework.boot:spring-boot-starter-web') {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
	}
	implementation 'org.springframework.boot:spring-boot-starter-jetty'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'


	implementation 'org.springframework:spring-context-indexer'

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.springframework.security:spring-security-test'
}

test {
	useJUnitPlatform()
	jvmArgs '-agentlib:native-image-agent=config-output-dir=src/main/resources/META-INF/native-image'
}

tasks.register('buildGitHubActions') {
    doLast {
        version = ""
    }
    finalizedBy build
}

compileJava{
	doFirst {
		//generate property on application.properties with timestamp, for use on spring
		def propertyFile = file "src/main/resources/application.properties"
		def props = new Properties()
		propertyFile.withReader { props.load(it) }
		props.setProperty('release', new Date().format('yyyyMMdd-HHmmss'))
		propertyFile.withWriter { props.store(it, null) }

	}
}

bootJar {
	mainClass.set('com.brunoshiroma.devtoolbelt.DevtoolbeltApplication')
}

graalvmNative {
	binaries {
		main{
			buildArgs.add('--libc=musl')
		}
	}
}